*$
* LM5156-Q1
*****************************************************************************
* (C) Copyright 2019 Texas Instruments Incorporated. All rights reserved.                                            
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of 
** merchantability or fitness for a particular purpose.  The model is
** provided solely on an "as is" basis.  The entire risk as to its quality
** and performance is with the customer
*****************************************************************************
*
* This model is subject to change without notice. Texas Instruments
* Incorporated is not responsible for updating this model.
*
*****************************************************************************
*
** Released by: Texas Instruments Inc.
* Part: LM5156-Q1
* Date: 09DEC2019
* Model Type: TRANSIENT
* Simulator: PSPICE
* Simulator Version: 16.2.0.p001
* EVM Order Number: NA
* EVM Users Guide: NA
* Datasheet: SNVSBI7 â€“JULY 2019
* Topologies Supported: Boost, Flyback, SEPIC
*
* Model Version: Final 1.00
*
*****************************************************************************
*
* Updates:
*
* Final 1.10
* Removed third party models for PSPICE for TI compatibility
*
* Final 1.00
* Release to Web.
*
*****************************************************************************
*
* Model Usage Notes:
* The subcircuits are used by the components present in LM5155-Q1 EVM schematic
*
*****************************************************************************
*.MODEL PMEG060V100EPD D( IS=1e-15 TT=10p Rs=0.016 N=.28  )
*$
*.MODEL PMEG045V100EPDAZ D( IS=1e-15 TT=10p Rs=0.016 N=.28   )
*$
*.SUBCKT POWERMOS Drain Gate Source PARAMS: RDSON=5.7m Ciss=930p Crss=14p Coss=210p VSP=1.6 RG=2
*C_C1         Source Da  {Coss} IC=0
*R_R1         Da Drain  10
*C_C2         Ga Drain  {Crss} IC=0
*R_R2         Gate Ga {RG}
*C_C3         Ga Source  {Ciss} IC=0
*D_D1         Source Db Dbreak
*R_R3         Db Drain 1m
*S_switchM         Drain Source Ga Source _switchM
*RS_switchM         Ga Source 100Meg
*.MODEL         _switchM VSWITCH Roff=100e6 Ron={RDSON} Voff=1.1 Von={VSP}
*.model Dbreak D Is=1e-14 Cjo=0.1pF Rs=0.1
*.ends POWERMOS
*$
.SUBCKT CESR IN OUT
+ PARAMS:  C=100u ESR=0.01 X=1 IC=0
C   IN 1  {C*X} IC={IC}
RESR    1 OUT {ESR/X}
.ENDS CESR
*$
.SUBCKT LDCR IN OUT
+ PARAMS:  L=1u DCR=0.01 IC=0
L   IN 1  {L} IC={IC}
RDCR    1 OUT {DCR}
.ENDS LDCR
*$
.SUBCKT 750317933 1 2 3 4 5 6
Rpri 1 100 1
Lpri 2 100 21u  ; 
RP 100 2 100MEG
E1 5_1 4 100 2 1
F1 100 2 V_VM1 1
RS1 6_1 3  1; 
V_VM1 5_1 6_1 DC=0
E2 20 6 100 2 0.5
F2 100 2 V_VM2 0.5
RS2 21 5  1u; 
V_VM2 20 21 DC=0
.ENDS 750317933
*$
.SUBCKT LMV431A A K Fdbk
V_V2         N59715 A 1.24
G_G4         K A TABLE { V(STAGE2, A) } 
+ ( (-10,0)(0,0)(80m,80m)(10,81m) )
R_R1         A STAGE1  1  
R_R2         A STAGE2  1  
C_C2         A STAGE1  225.34u 
C_C3         A STAGE2  73.8754n  
G_G1         A STAGE1 Fdbk N59715 4
X_D1          A STAGE1 DC_1mV_1A_1V_1nA
G_G3         A STAGE2 STAGE1 A 1
X_D2          STAGE1 N59689 DC_1mV_1A_1V_1nA
X_D3          A K DC_1mV_1A_1V_1nA
V_V1         N59689 A 80m
.ENDS LMV431A
*$
.SUBCKT DC_1mV_1A_1V_1nA A C
G1 A C TABLE { V(A, C) } ( (-1,-1n)(0,0)(1m,1) (2m,10) (3m,1000) )
.ends DC_1mV_1A_1V_1nA 
*$
.SUBCKT CIN 1 2 PARAMS: C = 0.33u ESR = 0.542 IC = 0
C 1 3 {C} IC = {IC} 
Resr 3 2 {ESR}
.ENDS CIN
*$
.SUBCKT PS2811 A K E C
D1 A D LED
Vsense D K 0
Hd   R 0 Vsense 1
Rd   R T 100K
Cd   T 0 20p
Gctr C B TABLE
+ {If(V(T)<=5m,
+ ((-4640000000*PWR(V(T),5)+61850000*PWR(V(T),4)-
+ 381800*PWR(V(T),3)+1378*PWR(V(T),2)+1.028*V(T)-0.0001275)/550),
+ ((2555000*PWR(V(T),5)-351400*PWR(V(T),4)+
+ 18100*PWR(V(T),3)-443.3*PWR(V(T),2)+5.628*V(T)-0.003168)/550))}
+ (0,0) (0.1m, 0.1m)
Q1 C B E detector
.model LED D IS=1p N=1.999644 RS=0 BV=6 IBV=10u
+ CJO=10p EG=1.424 TT=500n
.model detector NPN IS=100p BF=550 NF=1.3 BR=50 TF=1n TR=1.5n
+ CJE=7p CJC=13p VAF=100 ISS=0 CJS=1p
.ends PS2811
*$
.subckt  DD_1280_7448700015_1u5  1  2  3  4  PARAMS:
+  Cww=7.693p
+  Rp1=3222.084477671
+  Cp1=4.951310638861p
+  Lp1=1.27122123005u
+  Rp2=3222.084477671
+  Cp2=4.951310638861p
+  Lp2=1.27122123005u
+  RDC1=0.014750000089407
+  RDC2=0.014750000089407
+  K=0.959440559585571
C_C1  2  1    {Cww/2}
R_R50  2  1  5000g
C_C2  3  4    {Cww/2}
C_C5  3  2    {Cp1}
R_R1  3  N05454    {RDC1}
R_R2  3  2    {Rp1}
L_L1  N05454  2    {Lp1}
L_L2  N05750  1    {Lp2}
C_C6  4  1    {Cp2}
R_R3  4  1    {Rp2}
R_R4  4  N05750    {RDC2}
Kn_K1  L_L1  L_L2      {K}
.ends DD_1280_7448700015_1u5
*$
